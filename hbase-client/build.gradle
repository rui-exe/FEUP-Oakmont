plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.0'
    id 'com.google.protobuf' version '0.9.4'
}

def hadoopVersion = project.findProperty("hadoopVersion") ?: "3.2.2"
def hbaseVersion = project.findProperty("hbaseVersion") ?: "2.5.8"

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.apache.hadoop:hadoop-common:${hadoopVersion}"
    implementation "org.apache.hbase:hbase:${hbaseVersion}"
    implementation "org.apache.hbase:hbase-common:${hbaseVersion}"
    implementation "org.apache.hbase:hbase-client:${hbaseVersion}"
    implementation "org.apache.hbase:hbase-server:${hbaseVersion}"
    implementation group: 'org.json', name: 'json', version: '20240303'
    implementation 'io.grpc:grpc-netty-shaded:1.63.0'
    implementation 'io.grpc:grpc-protobuf:1.63.0'
    implementation 'io.grpc:grpc-stub:1.63.0'
    implementation 'org.apache.tomcat:annotations-api:6.0.53' // necessary for Java 9+
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

jar {
    manifest {
        attributes 'Main-Class': 'hbaseclient.HbaseClientServer'
    }
}

shadowJar {
    project.configurations.implementation.canBeResolved = true
    configurations = [project.configurations.implementation]
    archiveBaseName.set("hbase-client")
    archiveClassifier.set('')
    archiveVersion.set('')
    mergeServiceFiles()
}

protobuf {
  protoc {
    artifact = "com.google.protobuf:protoc:3.25.1"
  }
  plugins {
    grpc {
      artifact = 'io.grpc:protoc-gen-grpc-java:1.63.0'
    }
  }
  generateProtoTasks {
    all()*.plugins {
      grpc {}
    }
  }
}